\AgdaHide{
\begin{code}
module replaceMin where

open import Size
open import Data.Nat renaming (_⊔_ to max)
open import Data.Product
open import Data.Sum
open import Relation.Binary.PropositionalEquality
open ≡-Reasoning

infixl 30 _⊛_
infixr 30 _⇒_
infixr 50 _⊗_
\end{code}
}

\section{Delayed Computations}
As we have seen, the Haskell implementation cannot automatically be transferred to Agda, because the termination cannot be guaranteed.
For that reason, we introduce a mechanism, which allows delaying computations.
Beside that, the termination must take these delays into account.

Let us start with thinking about delaying computations.
Types do not have any dependence on time: all their inhabitants are always available.
To make them depend on time, we look at types indexed by some type of times.
We call them \emph{timed types}.

\subsection{Timed Types}
The main ingredient for timed types, is thus the indexing type.
The most straightforward option would be the natural, but we shall refrain to use them.
Instead, we use \AF{Size} for the indices.
Before explaining why, let us precisely define what timed types are.

\begin{code}
Time : Set
Time = Size

TimedSet = Time → Set
\end{code}

In the remainder of this section, we explain how to use timed types.
For that we introduce several combinators: some of them to construct such types, some of them for actual programming.
The simplest lifts operations on types to timed types.

\begin{code}
_⇒_ : TimedSet → TimedSet → TimedSet
(A ⇒ B) i = A i → B i

_⊗_ : TimedSet → TimedSet → TimedSet
(A ⊗ B) i = A i × B i

c : Set → TimedSet
c A i = A
\end{code}

Timed types can be turned into types.
This corresponds to universal quantification in first order logic.
If we think of a time type as a predicate on times, 

\begin{code}
□ : TimedSet → Set
□ A = {i : Time} → A i
\end{code}

To see what is going on, let us look at an example of a timed type.

\begin{code}
TimedNat : TimedSet
TimedNat = c ℕ
\end{code}

The natural numbers are generated by zero and the successor.
We can define these operations on the timed natural as well.
They are defined pointwise.

\begin{code}
Z : □ TimedNat
Z {i} = 0

S : □(TimedNat ⇒ TimedNat)
S {i} n = suc n
\end{code}

At every time, we have both zero and the successor of each timed natural number.
Other operations, for example the minimum, are defined in the same way.
Note that the function \AF{⊓} takes the minimum of two natural numbers.

\begin{code}
min : □(TimedNat ⇒ TimedNat ⇒ TimedNat)
min {i} n m = n ⊓ m
\end{code}

The last combinator represents delayed computations.
For this, we need to look more closely to Agda's mechanism of sized types.
An important operation on sizes is the order and we use it to define an order on types.

\begin{code}
Time< : Time → Set
Time< i = Size< i
\end{code}

The set \AF{Time<} \AB{i} represents the times smaller than \AB{i}.
With this order, we can now defined delayed computations.

\begin{code}
record ▻ (A : TimedSet) (i : Time) : Set where
  coinductive
  field force : {j : Time< i} → A j
open ▻ public
\end{code}

The only inhabitants \AF{▻} \AB{A} \AB{i} are those of \AB{A} \AB{j} for \AB{j} smaller than \AB{i}.
This means that something in \AB{A} \AB{i} is only accessible in \AF{▻} \AB{A} \AB{k} for \AB{k} greater than \AB{i}.
Or, in words, this means that inhabitants of \AB{A} are only available in \AF{▻} \AB{A} at later times.

Before discussing examples, let us first look at how to program with delayed computations.
First, we give \AF{▻} the structure of an applicative functor.

\begin{code}
pure : {A : TimedSet} → □ A → □(▻ A)
force (pure x) = x

_⊛_ : {A B : TimedSet} → □(▻(A ⇒ B) ⇒ ▻ A ⇒ ▻ B)
force (f ⊛ x) = force f (force x)
\end{code}

Secondly, we give a fixpoint combinator and here the order plays a crucial role.
The reason of that, is because it allows broader priductivity checks.
The semantics guarantee that there is no infinitely decreasing sequence of sizes.
In particular, if a size decrease in every recursive call of some function, then this map is productive.

\begin{code}
fix : {A : TimedSet} → □(▻ A ⇒ A) → □ A
▻fix : {A : TimedSet} → □(▻ A ⇒ A) → □ (▻ A)
fix f {i} = f (▻fix f {i})
force (▻fix f {i}) {j} = fix f {j}
\end{code}

We define the fixpoint of \AB{f} by repeatedly applying \AB{f}.
Since the argument of \AB{f} is delayed, we also need a delayed version of \AF{fix}, which we call \AF{▻fix}.
Note that this function calls \AF{fix} again, but the size decreases.
For that reason, this function is actually productive.

Now let us discuss an example of a timed type which uses \AF{▻}.

\begin{code}
data LazyNat : TimedSet where
  LZ : □ LazyNat
  LS : □(▻ LazyNat ⇒ LazyNat)
\end{code}

Again we have a constructor \AIC{LZ}, representing zero, and a constructor \AIC{LS}, representing the successor.
However, the successor is different, because it only accepts arguments from some previous time.
Using \AF{pure}, we can still embed the natural numbers into the lazy numbers.

\begin{code}
ℕ-to-LazyNat : ℕ → □ LazyNat
ℕ-to-LazyNat zero = LZ
ℕ-to-LazyNat (suc n) = LS (pure (ℕ-to-LazyNat n))
\end{code}

This type is different from \AF{TimedNat}, which we define before.
Beside the natural numbers, \AF{LazyNat} also contains infinity.
Since \AIC{LS} has a lazy argument, we can take the fixpoint.

\begin{code}
infinity : □ LazyNat
infinity = fix LS
\end{code}

\section{Eliminating Traversals}
Now we have developed sufficient material to formalize the program.
We start by defining the relevant data types: a data type of natural numbers and a data type of trees.
Remember that we already defined \AF{TimedNat} as a constant family.
The data type of trees is defined the same way.

\begin{code}
data Tree : Set where
  Leaf : ℕ → Tree
  Node : Tree → Tree → Tree

TimedTree : TimedSet
TimedTree = c Tree
\end{code}

This data type has two constructors \AF{TLeaf} and \AF{TNode}.
We also have delayed versions of them.

\begin{code}
TLeaf : □(TimedNat ⇒ TimedTree)
TLeaf {i} n = Leaf n

TNode : □(TimedTree ⇒ TimedTree ⇒ TimedTree)
TNode {i} l r = Node l r

▻Leaf : □(▻ TimedNat ⇒ ▻ TimedTree)
▻Leaf n = pure Leaf ⊛ n

▻Node : □(▻ TimedTree ⇒ ▻ TimedTree ⇒ ▻ TimedTree)
▻Node t₁ t₂ = pure Node ⊛ t₁ ⊛ t₂
\end{code}

\begin{code}
fb-h : {T N : TimedSet} → □(▻ N ⇒ T ⊗ N) → □(▻(T ⊗ N) ⇒ T ⊗ N)
fb-h f x = f (pure proj₂ ⊛ x)

feedback : {T N : TimedSet} → □(▻ N ⇒ T ⊗ N) → □ T
feedback f = proj₁ (fix (fb-h f))
\end{code}

Now we want to define the help function \AF{rmb}, which will be the argument for \AF{feedback}.
We take \AF{⊳ Tree} for \AB{B} and \AF{SizedNat} for \AB{U}.
Note that we must use \AF{⊳ Tree}, because otherwise we would not be able to apply \AIC{Node} or \AF{⊳Node}.

\begin{code}
rmb : □(TimedTree ⇒ ▻ TimedNat ⇒ (▻ TimedTree) ⊗ TimedNat)
rmb (Leaf x) n = (▻Leaf n , x)
rmb (Node l r) n =
  let (l' , ml) = rmb l n
      (r' , mr) = rmb r n
  in (▻Node l' r' , min ml mr)

replaceMin : □(TimedTree ⇒ TimedTree)
replaceMin {i} t = force (feedback (rmb t))
\end{code}

\section{Proving with Sized Types}
Our next goal is to prove correctness.
Before doing that, we need to define timed predicates, timed relations, and combinators on them.

\begin{code}
TimedPredicate : TimedSet → Set₁
TimedPredicate A = {i : Time} → A i → Set

TimedRelation : TimedSet → TimedSet → Set₁
TimedRelation A B = {i : Time} → A i → B i → Set
\end{code}

Next we define universal quantification and for that we use dependent products.
Given a sized type \AB{A} and a predicate on \AB{A}, we get another sized type.

\begin{code}
∏ : (A : TimedSet) → TimedPredicate A → TimedSet
∏ A B i = (x : A i) → B x
\end{code}

Furthermore, for each sized type \AB{A}, we have a relation on \AB{A} representing equality.
For this we use propositional equality in Agda.

\begin{code}
eq : (A : TimedSet) → TimedRelation A A
eq A x y = x ≡ y

syntax eq A x y = x ≡[ A ]≡ y
\end{code}

\begin{code}
Time<Set : TimedSet
Time<Set i = Time< i

▻eq : (A : TimedSet) → TimedRelation (Time<Set ⇒ ▻ A) (Time<Set ⇒ ▻ A)
▻eq A {i} x y = {j : Time< i} → force (x j) {j} ≡[ A ]≡ force (y j)

syntax ▻eq A x y = x ∼[ A ]∼ y
\end{code}

\section{Correctness}

\subsection{Specification}
\begin{code}
replace : □(TimedTree ⇒ ▻ TimedNat ⇒ ▻ TimedTree)
replace (Leaf x) n = ▻Leaf n
replace (Node l r) n = ▻Node (replace l n) (replace r n) 
\end{code}

\begin{code}
min-tree : □(TimedTree ⇒ TimedNat)
min-tree (Leaf x) = x
min-tree (Node l r) = min (min-tree l) (min-tree r)
\end{code}

\begin{code}
replaceMin-spec : □(TimedTree ⇒ TimedTree)
replaceMin-spec t = force (replace t (pure (min-tree t)))

valid : □(TimedTree ⇒ TimedTree) → Set
valid f = □(∏ TimedTree (λ t → f t ≡[ TimedTree ]≡ replaceMin-spec t))
\end{code}

\subsection{Proof}

\AgdaAlign{
\begin{code}
rmb₁ : □(∏ TimedTree (λ t →
         □(∏ (▻ TimedNat) (λ n →
             proj₁ (rmb t n) ≡[ ▻ TimedTree ]≡ replace t n))))
rmb₁ (Leaf x) n = refl
rmb₁ (Node l r) n =
  begin
    proj₁ (rmb (Node l r) n)
\end{code}
\quad \AF{≡⟨} unfold \AF{rmb} \AF{⟩}
\AgdaHide{
\begin{code}
  ≡⟨ refl ⟩ -- unfold rmb
\end{code}
  }
\begin{code}
    ▻Node (proj₁ (rmb l n)) (proj₁ (rmb r n))
\end{code}
\quad \AF{≡⟨} induction hypothesis, \AF{rmb₁} \AB{l} \AB{n} \AF{⟩}
\AgdaHide{
\begin{code}
  ≡⟨ cong (λ x → ▻Node x (proj₁ (rmb r n))) (rmb₁ l n) ⟩
\end{code}}
\begin{code}
    ▻Node (replace l n) (proj₁ (rmb r n))
\end{code}
\quad \AF{≡⟨} induction hypothesis, \AF{rmb₁} \AB{r} \AB{n} \AF{⟩}
\AgdaHide{
\begin{code}
  ≡⟨ cong (▻Node (replace l n)) (rmb₁ r n) ⟩
\end{code}}
\begin{code}
    ▻Node (replace l n) (replace r n)
\end{code}
\quad \AF{≡⟨} fold \AF{replace} \AF{⟩}
\AgdaHide{
\begin{code}
  ≡⟨ refl ⟩ -- unfold replace
\end{code}}
\begin{code}
    replace (Node l r) n
  ∎
\end{code}
}

\AgdaAlign{
\begin{code}
rmb₂ : □(∏ TimedTree (λ t →
         □(∏ (▻ TimedNat) (λ n →
             proj₂ (rmb t n) ≡[ TimedNat ]≡ min-tree t)))
        )
rmb₂ (Leaf x) n = refl
rmb₂ (Node l r) n =
  begin
    proj₂ (rmb (Node l r) n)
\end{code}
\quad \AF{≡⟨} unfold \AF{rmb} \AF{⟩} 
\AgdaHide{
\begin{code}
  ≡⟨ refl ⟩ -- unfold rmb
\end{code}}
\begin{code}
    min (proj₂ (rmb l n)) (proj₂ (rmb r n))
\end{code}
\quad \AF{≡⟨} induction hypothesis, \AF{rmb₂} \AB{r} \AB{n} \AF{⟩}
\AgdaHide{
\begin{code}
  ≡⟨ cong (min (proj₂ (rmb l n))) (rmb₂ r n) ⟩
\end{code}}
\begin{code}
    min (proj₂ (rmb l n)) (min-tree r)
\end{code}
\quad \AF{≡⟨} induction hypothesis, \AF{rmb₂} \AB{l} \AB{n} \AF{⟩}
\AgdaHide{
\begin{code}
  ≡⟨ cong (λ x → min x (min-tree r)) (rmb₂ l n) ⟩
\end{code}}
\begin{code}
    min (min-tree l) (min-tree r)
  ∎
\end{code}
}

\begin{code}
replace< : □(TimedTree ⇒ ▻ TimedNat ⇒ Time<Set ⇒ ▻ TimedTree)
replace< t n j = replace t n

replace-pure : □(TimedTree ⇒ ▻ TimedNat ⇒ Time<Set ⇒ ▻ TimedTree)
replace-pure t n j = replace t (pure (force n {j}))
\end{code}

\AgdaAlign{
\begin{code}
⊳replace : □(∏ TimedTree (λ t →
             □(∏ (▻ TimedNat) (λ n →
                 replace< t n ∼[ TimedTree ]∼ replace-pure t n)))
            )
⊳replace (Leaf x) n = refl
⊳replace (Node l r) n =
  begin
    force (replace (Node l r) n)
  ≡⟨ refl ⟩
    force (▻Node (replace l n) (replace r n))
  ≡⟨ refl ⟩
    force (pure Node ⊛ (replace l n) ⊛ (replace r n))
  ≡⟨ refl ⟩
    force (pure Node) (force (replace l n)) (force (replace r n))
  ≡⟨ cong (λ z → force (pure Node) z (force (replace r n))) (⊳replace l n) ⟩
    force (pure Node) (force (replace l (pure (force n)))) (force (replace r n))
  ≡⟨ cong (λ z → force (pure Node) (force (replace l (pure (force n)))) z) (⊳replace r n) ⟩
    force (replace (Node l r) (pure (force n)))
  ∎
\end{code}}

\AgdaAlign{
\begin{code}
rm-correct : valid replaceMin
rm-correct t =
  begin
    replaceMin t
\end{code}
\quad \AF{≡⟨} unfold \AF{replaceMin}, \AF{feedback} \AF{⟩}
\AgdaHide{
\begin{code}
  ≡⟨ refl ⟩ -- unfold replaceMin, feedback
\end{code}}
\begin{code}
    force (proj₁ (fix (λ x → rmb t (pure proj₂ ⊛ x))))
\end{code}
\quad \AF{≡⟨} unfold \AF{fix} \AF{⟩}
\AgdaHide{
\begin{code}
  ≡⟨ refl ⟩ -- unfold fix
\end{code}}
\begin{code}
    force (proj₁ (rmb t (pure proj₂ ⊛ ▻fix (fb-h (rmb t)))))
\end{code}
\quad \AF{≡⟨} rewrite \AF{rmb₁} \AF{⟩}
\AgdaHide{
\begin{code}
  ≡⟨ cong (λ x → force x)
          (rmb₁ t ((pure proj₂ ⊛ ▻fix (fb-h (rmb t))))) ⟩
\end{code}}
\begin{code}
    force (replace t (pure proj₂ ⊛ ▻fix (fb-h (rmb t))))
\end{code}
\quad \AF{≡⟨} rewrite \AF{⊳replace} \AF{⟩}
\AgdaHide{
\begin{code}
  ≡⟨ ⊳replace t (pure proj₂ ⊛ ▻fix (fb-h (rmb t))) ⟩
\end{code}}
\begin{code}
    force (replace t (pure (force (pure proj₂ ⊛ ▻fix (fb-h (rmb t))))))
\end{code}
\quad \AF{≡⟨} unfold \AF{pure} \AF{⟩}
\AgdaHide{
\begin{code}
  ≡⟨ refl ⟩ -- unfold pure
\end{code}}
\begin{code}
    force (replace t (pure (proj₂ (fix (fb-h (rmb t))))))
\end{code}
\quad \AF{≡⟨} unfold \AF{fix} \AF{⟩}
\AgdaHide{
\begin{code}
  ≡⟨ refl ⟩ -- unfold fix
\end{code}}
\begin{code}
    force (replace t (pure (proj₂ (rmb t ((pure proj₂ ⊛ ▻fix (fb-h (rmb t))))))))
\end{code}
\quad \AF{≡⟨} rewrite \AF{rmb₂} \AF{⟩}}
\AgdaHide{
\begin{code}
  ≡⟨ cong (λ x → force (replace t (pure x)))
          (rmb₂ t ((pure proj₂ ⊛ ▻fix (fb-h (rmb t))))) ⟩
\end{code}}
\begin{code}
    force (replace t (pure (min-tree t)))
  ∎
\end{code}
}
